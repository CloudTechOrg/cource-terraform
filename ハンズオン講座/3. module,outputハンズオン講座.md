# 概要
本ハンズオンではmoduleブロックを用いてdev prod環境を構築していこうと思います。
ではやっていきます。

## moduleのフォルダを作成
ではモジュール用にフォルダを作成します。

1. C:\Terrafrom\module-output-handson\modules\web


## moduleとして利用するようにファイルを分割
ではwebという名前のモジュールを作成していきます \
今までmain.tfにvariableブロックやresourceブロックなど全て書いていましたが、 \
variableブロックを`variables.tf`にまとめていきたいと思います。

これによってモジュールを利用するときにこのモジュールがどのような変数を入力できるのかわかりやすくなります。

では`main.tf`と`variables.tf`を作成します。

main.tf
```terraform
provider "aws" {
 region = "ap-northeast-1"
}

locals{
    app_name = "web"
    name_prefix = "${var.env}-${local.app_name}"
}

resource "aws_vpc" "web_vpc" {

  cidr_block = "10.0.0.0/16"
  tags = {
    Name = "${local.name_prefix}-vpc"
  }
}

resource "aws_subnet" "web_subnet" {

  vpc_id = aws_vpc.web_vpc.id
  map_public_ip_on_launch = true

  cidr_block = "10.0.0.0/24"
  tags = {
    Name = "${local.name_prefix}-public_subnet"
  }
}

resource "aws_route_table" "web_public_rtb" {
  vpc_id = aws_vpc.web_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.web_igw.id
  }

  tags = {
    Name = "${local.name_prefix}-public-rtb"
  }
}

resource "aws_route_table_association" "web_public_rtb_assoc" {
  subnet_id      = aws_subnet.web_subnet.id
  route_table_id = aws_route_table.web_public_rtb.id
}

resource "aws_internet_gateway" "web_igw" {
  vpc_id = aws_vpc.web_vpc.id

  tags = {
    Name = "${local.name_prefix}-igw"
  }
}

resource "aws_security_group" "web_sg" {
  vpc_id = aws_vpc.web_vpc.id

  name        = "${local.name_prefix}-sg"
  description = "Allow HTTP access from my IP"

  ingress {
    description = "Allow HTTP traffic from my IP"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["${var.myip}/32"] # var.myipからのHTTPアクセスを許可
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${local.name_prefix}-sg"
  }
}

resource "aws_instance" "web_ec2" {
  ami                         = "ami-094dc5cf74289dfbc" 
  instance_type               = "t2.micro"
  security_groups             = [aws_security_group.web_sg.id]
  subnet_id = aws_subnet.web_subnet.id

  user_data = <<-EOF
#!/bin/bash
dnf update -y
dnf install -y nginx
systemctl enable --now nginx
cat <<EOG > /usr/share/nginx/html/index.html
    <div style="text-align:center; font-size:1.5em; color:#333; margin:20px; line-height:1.8;">
        <b>env: ${var.env}</b><br>
        <b>app_name: ${local.app_name}</b><br>
        <b>name_prefix: ${local.name_prefix}</b><br>
        <b>myip: ${var.myip}</b>
    </div>
EOG
  EOF

  tags = {
    Name = "${local.name_prefix}-ec2"
  }
}
```

variables.tf
```
variable "env" {
    type = string
}

variable "myip" {
    type = string
}
```

## outputブロックを作成
ec2のパブリックIPを利用したいのでoutputブロックでパブリックIPをoutputブロックを作成します

output.tf
```
output "web_ec2_public_ip" {
  value = aws_instance.web_ec2.public_ip
}
```

これで子モジュールの作成が完了しました！
では呼び出すもとのルートモジュールを作成していきます。
まず開発環境から作成していきます

## 開発環境用のフォルダの作成
1. C:\Terrafrom\module-output-handson\env\dev
2. main.tfを作成

main.tf
```terraform
module "web" {
  source = "../../modules/web"

  myip=var.myip
  env=var.env
}
```

3. variables.tfを作成
modules/web/variables.tfの内容をコピーして作成

4. terraform.tfvarsを作成

https://www.whatismyip.com/
```terraform
myip="自分のIPアドレス"
env="dev"
```

5. output.tfを作成
```
output "web_addr" {
  value = "http://${module.web.web_ec2_public_ip}"
}
```

## 開発環境の作成

1. module-output-handson/env/devでパワーシェルを開き環境変数でアクセスキーとシークレットを設定します。

2. terraform initを行います。
 modulesフォルダを確認
3. terraform planを実行
4. terraform applyを実行
5. WEBへのアクセス確認
6. コンソールでresourceの確認

## 商用環境の作成

1. env/devをコピーしてprodを作成tfst
2. tfstateファイルを削除
3. tfvarsファイルを編集しenv="prod"にする
4. ターミナルのカレントディレクトリをenv/prodに移動
5. terraform initを行います。
6. terraform planを実行
7. terraform applyを実行
8. WEBへのアクセス確認
9.  コンソールでresourceの確認

## リソースの削除
terraform destroyでリソースの削除をします。
1. prod環境の削除
2. dev環境の削除