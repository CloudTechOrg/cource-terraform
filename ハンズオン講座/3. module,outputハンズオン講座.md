# 概要
本ハンズオンではmoduleを用いて複数の環境を構築していこうと思います。
ではやっていきます。

## moduleのフォルダを作成
ではハンズオン用のフォルダ作っていきましょう。
C:\Terrafrom\Handson\module-output
今回はモジュールブロック、outputブロックをメインで扱いますのでmodule-outputとしたいと思います。


## moduleとして利用するようにファイルを分割
ではwebという名前のモジュールを作成していきます \

モジュールを格納するようのフォルダとしてmodulesを作成しましょう

ここにモジュールを作成していきます。
モジュールの生身としては前回のハンズオンで扱ったWebサイトで作っていこうと思います。

ではまずmain.tfから作成していきます。
動画概要欄のmain.tfの中身コピーして貼り付けてください

前回はmain.tfにvariableブロックやresourceブロックなど全て書いていましたが、 \
variableブロックを`variables.tf`にまとめていきたいと思います。

これによってモジュールを利用するときにこのモジュールがどのような入力変数を扱えるのかわかりやすくなります。
ファイルは分けていますが、コードの内容は前回のハンズオンで扱った内容となります。

では`main.tf`と`variables.tf`を作成します。

main.tf
```terraform
provider "aws" {
 region = "ap-northeast-1"
}

locals{
    app_name = "web"
    name_prefix = "${var.env}-${local.app_name}"
}

resource "aws_vpc" "web_vpc" {

  cidr_block = "10.0.0.0/16"
  tags = {
    Name = "${local.name_prefix}-vpc"
  }
}

resource "aws_subnet" "web_subnet" {

  vpc_id = aws_vpc.web_vpc.id
  map_public_ip_on_launch = true

  cidr_block = "10.0.0.0/24"
  tags = {
    Name = "${local.name_prefix}-public_subnet"
  }
}

resource "aws_route_table" "web_public_rtb" {
  vpc_id = aws_vpc.web_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.web_igw.id
  }

  tags = {
    Name = "${local.name_prefix}-public-rtb"
  }
}

resource "aws_route_table_association" "web_public_rtb_assoc" {
  subnet_id      = aws_subnet.web_subnet.id
  route_table_id = aws_route_table.web_public_rtb.id
}

resource "aws_internet_gateway" "web_igw" {
  vpc_id = aws_vpc.web_vpc.id

  tags = {
    Name = "${local.name_prefix}-igw"
  }
}

resource "aws_security_group" "web_sg" {
  vpc_id = aws_vpc.web_vpc.id

  name        = "${local.name_prefix}-sg"
  description = "Allow HTTP access from my IP"

  ingress {
    description = "Allow HTTP traffic from my IP"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["${var.myip}/32"] # var.myipからのHTTPアクセスを許可
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${local.name_prefix}-sg"
  }
}

resource "aws_instance" "web_ec2" {
  ami                         = "ami-094dc5cf74289dfbc" 
  instance_type               = "t2.micro"
  security_groups             = [aws_security_group.web_sg.id]
  subnet_id = aws_subnet.web_subnet.id

  user_data = <<-EOF
#!/bin/bash
dnf update -y
dnf install -y nginx
systemctl enable --now nginx
cat <<EOG > /usr/share/nginx/html/index.html
    <div style="text-align:center; font-size:1.5em; color:#333; margin:20px; line-height:1.8;">
        <b>env: ${var.env}</b><br>
        <b>app_name: ${local.app_name}</b><br>
        <b>name_prefix: ${local.name_prefix}</b><br>
        <b>myip: ${var.myip}</b>
    </div>
EOG
  EOF

  tags = {
    Name = "${local.name_prefix}-ec2"
  }
}
```

variables.tf
```
variable "env" {
    type = string
}

variable "myip" {
    type = string
}
```



## outputブロックを作成
ec2のパブリックIPをルートモジュール側で利用したいのでoutputブロックでパブリックIPを出力します。
どの値が出力されるのかわかりやすいようにoutput.tfとしてファイルを作成します。

output.tf
```
output "web_ec2_public_ip" {
  value = aws_instance.web_ec2.public_ip
}
```

出力変数名としてweb_ec2_public_ipとして
値としてec2のパブリックＩＰを作成しています

これで子モジュールの作成が完了しました！
では呼び出すもとのルートモジュールを作成していきます。
まず開発環境用のルートモジュールから作成していきます

## 開発環境用のフォルダの作成
1. C:\Terrafrom\module-output\env\dev
2. main.tfを作成

では作成したフォルダでmain.tf作成していきます。

main.tf
```terraform
module "web" {
  source = "../../modules/web"

  myip=var.myip
  env=var.env
}
```
モジュールブロック記載していきます。ソースとしてローカルの先ほど作成したモジュールを指定します。
そして子モジュールに渡す変数としてルートモジュールの入力変数を渡しています。
ここ少しややこしいのでしっかり理解しましょう。
ルートモジュールで設定している入力変数、variable変数を子モジュールの入力変数にそのまま渡すという設定がここでされています。

3. variables.tfを作成
modules/web/variables.tfの内容をコピーして作成

ルートモジュールでは子モジュールと同じ入力変数を使いたいので
子moduleないのvariables.tfをコピーします。

4. terraform.tfvarsを作成

次に入力変数へ値を設定します。
今回はterraform.tfvarsというファイルを作成し利用します。

https://www.whatismyip.com/
```terraform
myip="自分のIPアドレス"
env="dev"
```

次にルートモジュール用の出力変数を設定します。

5. output.tfを作成
```
output "web_addr" {
  value = "http://${module.web.web_ec2_public_ip}"
}
```

outputブロックで設定します。
出力として、webサイトのアドレスをそのまま、コンソールに出力するようにしようと思います。
子モジュールwebのoutputブロックで設定した出力変数を呼び出すにはmodule.web.出力変数名という風に記載します。
そしてhttp://を設定することで直接アクセスできるようにしています。


はいこれで開発環境をデプロイする準備ができました、terraform でデプロイをしていきましょう

## 開発環境の作成

1. module-output-handson/env/devでパワーシェルを開きましょう
環境変数でアクセスキーとシークレットを設定します。

2. terraform initを行います。
 modulesフォルダを確認
terraform initしますとmodulesというフォルダが作成されます。
そしてマッピング情報がここに記載されます。

続いて terraform plan applyしていきます。

3. terraform planを実行
4. terraform applyを実行

apply実行するとWebサイトのアドレスがが表示されました。
こちらctrl +クリックでアクセスしてみましょう

はいこのようにアクセスすることができました

6. コンソールでresourceの確認

EC2のダッシュボードみるとほげほげという名前でec2作成されていることがわかります。


では次に商用環境を作成していきましょう。

## 商用環境の作成

1. env/prodを作成
2. 必要なファイルをコピーします。　
3. tfvarsファイルを編集しenv="prod"にする
4. ターミナルのカレントディレクトリをenv/prodに移動
5. terraform initを行います。
6. terraform planを実行
7. terraform applyを実行
8. WEBへのアクセス確認
9.  コンソールでresourceの確認

## リソースの削除
terraform destroyでリソースの削除をします。
こちらはtfstateファイルで管理しているすべてのリソースを削除するコマンドになりますので、少し危険なコマンドになっています。使うときには十分に配慮して利用しましょう。

1. prod環境の削除
2. dev環境の削除